%start BackgroundUpdaterMap::Init
%class  BackgroundUpdater
%header BackgroundUpdater.h

%map BackgroundUpdaterMap
%%

Init
{
    Init Idle {}
}

Idle
Entry { setIdle(YES); }
Exit { setIdle(NO); }
{
    Start IndexingByShortName { prepareToIndexByShortName(); }
    AuditGames(games: NSArray *) AuditingGames { prepareToAuditGames(games); }
}

IndexingByShortName
{
    DoWork nil { indexByShortName(); }
    WorkDone UpdatingGameList { prepareToUpdateGameList(); }
}

UpdatingGameList
{
    DoWork nil { updateGameList(); }
    WorkDone AuditingGames { prepareToAuditAllGames(); }
}

AuditingGames
Entry { setAuditing(YES); }
Exit { setAuditing(NO); }
{
    DoWork nil { auditGames(); }
    AbortAudit Idle { cleanUp(); }
    WorkDone Idle { cleanUp(); }
}

Default
{
    Pause push(Paused) { }
    DoWork nil { }
}

Paused
Entry { saveState(); }
Exit { restoreState(); }
{
    Pause nil { }
    Resume pop() { }
}

%%
